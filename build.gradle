plugins {
    id "org.springframework.boot" version "2.3.0.RELEASE"
    id 'io.spring.dependency-management' version "1.0.9.RELEASE"
    id 'java'
    id 'war'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id "com.github.spotbugs" version "4.2.4"
}
//apply plugin: 'java'
//apply plugin: 'idea'
//apply plugin: 'war'
//apply plugin: 'org.springframework.boot'
//apply plugin: 'io.spring.dependency-management'

version = '0.0.1'
group = 'com.hello'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-cache'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-devtools'
//    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'

    implementation group: 'com.github.ulisesbocchio', name: 'jasypt-spring-boot-starter', version: '3.0.2'

    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.4.0'
//    implementation group: 'org.springdoc', name: 'springdoc-openapi-data-rest', version: '1.4.0'
    implementation group: 'org.springdoc', name: 'springdoc-openapi-security', version: '1.4.0'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
    implementation group: 'org.modelmapper', name: 'modelmapper', version: '2.3.0'
    implementation group: 'com.h2database', name: 'h2'

    implementation group: 'javax.cache', name: 'cache-api', version: '1.1.1'
    implementation group: 'org.ehcache', name: 'ehcache', version: '3.8.1'

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.12'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.12'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.3.0'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // JAX-B dependencies for JDK 9+
//    implementation "javax.xml.bind:jaxb-api:2.2.11"
//    implementation "com.sun.xml.bind:jaxb-core:2.2.11"
//    implementation "com.sun.xml.bind:jaxb-impl:2.2.11"
//    implementation "javax.activation:activation:1.1.1"
}

//test.onlyIf { !project.hasProperty('skipTests') }

tasks.named('test', Test) {
    useJUnitPlatform()
}
test {
    onlyIf { !project.hasProperty('skipTest') }
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}
check {
    onlyIf { !project.hasProperty('skipCheck') }
    dependsOn jacocoTestCoverageVerification
}
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.5
            }
        }
    }
}
checkstyle {
    toolVersion '8.26'
    configFile file("config/checkstyle/checkstyle.xml")
}
checkstyleMain {
    onlyIf { !project.hasProperty('skipCheck') }

    source = 'src/main/java'
}
checkstyleTest {
    onlyIf { !project.hasProperty('skipCheck') }

    source = 'src/test/java'
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/checkstyle/checkstyle.xsl')
    }
}
spotbugs {
}
spotbugsMain {
    onlyIf { !project.hasProperty('skipCheck') }

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
spotbugsTest {
    onlyIf { !project.hasProperty('skipCheck') }

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
